// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx, nx 等同于多路复用器中的 sel, 设置最终 x 的值.
    Mux16(a=x, b=false, sel=zx, out=x1);
    Not16(in=x1, out=nx1);
    Mux16(a=x1, b=nx1, sel=nx, out=x2); // x

    // zy, ny 等同于多路复用器中的 sel, 设置最终 y 的值.
    Mux16(a=y, b=false, sel=zy, out=y1);
    Not16(in=y1, out=ny1);
    Mux16(a=y1, b=ny1, sel=ny, out=y2); // y

    // f, no 等同于多路复用器中的 sel, 设置最终的操作.
    Add16(a=x2, b=y2, out=add); // x + y
    And16(a=x2, b=y2, out=and); // x & y
    Mux16(a=and, b=add, sel=f, out=out1);
    Not16(in=out1, out=out2);
    Mux16(a=out1, b=out2, sel=no, out=out, out[0..7]=outl, out[8..15]=outh, out[15]=outm); // out, 可以有多个输出, 类似解构!
    
    // 结果的每一位都是0, 结果就是0.
    Or8Way(in=outl, out=ooutl);
    Or8Way(in=outh, out=oouth);
    Or(a=ooutl, b=oouth, out=oout);
    Not(in=oout, out=zr);

    // 结果的最高位为1, 结果就是负数.
    Or(a=outm, b=false, out=ng);
}